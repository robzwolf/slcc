import org.apache.tools.ant.DirectoryScanner

plugins {
    id "java"
    id "application"
}

group "com.scottlogic.hackathon-contestant"
version "1.0-SNAPSHOT"

task printProps {
    doFirst {
        println mavenProxyUrl
    }
}

repositories {
    if(project.hasProperty('mavenProxyUrl')){
        maven {
            url mavenProxyUrl
        }
    } else{
        mavenCentral()
    }

    flatDir {
        dirs "libs"
    }
}

dependencies {
     compile name: 'game-1.0-SNAPSHOT'
     compile name: 'jraph-0.1.2'
     compile name: 'client-1.0-SNAPSHOT'
     compile name: 'default-bots-1.0-SNAPSHOT-obfuscated'
     compile name: 'slf4j-simple-1.7.12'
     compile name: 'slf4j-api-1.7.12'
     compile name: 'jansi-1.11'
     compile name: 'commons-cli-1.4'
     compile name: 'game-engine-1.0-SNAPSHOT'
     compile name: 'jul-to-slf4j-1.7.12'
     compile name: 'jackson-core-2.6.3'
     compile name: 'jackson-annotations-2.6.3'
     compile name: 'jackson-databind-2.6.3'
     testCompile name: 'junit-4.11'
     testCompile name: 'hamcrest-core-1.3'
}

mainClassName = project.hasProperty('mainClass') ? project.mainClass : 'com.contestantbots.team.ExampleBot'

run {
    standardInput = System.in
}

tasks.withType(Wrapper) {
    doLast {
        boolean found = false
        batchScript.text = batchScript.readLines().collect {
            if(!found && it.startsWith('set APP_HOME')) {
                found = true
                return it + '\r\n' +
                        '\r\n' +
                        'FOR /F "tokens=1* delims== usebackq" %%A IN ("%APP_HOME%\\gradle.properties") DO (\r\n' +
                        '    IF "%%A"=="org.gradle.java.home" set JAVA_HOME=%%B\r\n' +
                        ')\r\n' +
                        '\r\n' +
                        'echo JAVA_HOME: %JAVA_HOME%\r\n'
            } else return it
        }.join('\r\n')
        if(!found) {
            throw new IllegalStateException("Insertion point in ${batchScript.name} not found.")
        }

        found = false
        scriptFile.text = scriptFile.readLines().collect {
            if(!found && it.contains('CLASSPATH=')) {
                found = true
                return it + '\n' +
                        '\n' +
                        'while IFS="=" read -r k v; do\n' +
                        '    if [ "$k" = "org.gradle.java.home" ]; then\n' +
                        '        export JAVA_HOME="$v"\n' +
                        '        UNAME=`uname`\n' +
                        '        if [ "$cygwin" = "true" -o "$msys" = "true" -o "${UNAME#MSYS}" != "$UNAME" ]; then\n' +
                        '            export JAVA_HOME=`cygpath "$JAVA_HOME"`\n' +
                        '        fi\n' +
                        '    fi\n' +
                        'done < "$APP_HOME/gradle.properties"\n' +
                        '\n' +
                        'echo "JAVA_HOME: $JAVA_HOME"\n'
            } else return it
        }.join('\n')
        if(!found) {
            throw new IllegalStateException("Insertion point in ${scriptFile.name} not found.")
        }

    }
}

